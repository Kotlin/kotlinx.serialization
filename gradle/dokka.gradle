/*
 * Copyright 2017-2020 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license.
 */

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

def documentedSubprojects = ["kotlinx-serialization-core",
                             "kotlinx-serialization-json",
                             "kotlinx-serialization-cbor",
                             "kotlinx-serialization-properties",
                             "kotlinx-serialization-hocon",
                             "kotlinx-serialization-protobuf"]
subprojects {
    if (!(name in documentedSubprojects)) return
    apply plugin: 'org.jetbrains.dokka'
    tasks.named('dokkaHtml') {
        outputDirectory = file("build/dokka")
        dokkaSourceSets {
            configureEach {
                includes.from(rootProject.file('dokka/moduledoc.md').path)

                perPackageOption {
                    prefix.set("kotlinx.serialization")
                    reportUndocumented.set(false)
                    skipDeprecated.set(true)
                }

                perPackageOption {
                    prefix.set("kotlinx.serialization.internal")
                    suppress.set(true)
                    reportUndocumented.set(false)
                }
            }
        }
    }
}

task dokka(dependsOn: dokkaHtmlMultiModule) {}
