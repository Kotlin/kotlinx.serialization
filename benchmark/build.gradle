apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'idea'
apply plugin: "net.ltgt.apt"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "me.champeau.gradle.jmh"

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    create('jvmImplementation') {
        if (rootProject.ext.jvm_ir_target_enabled) {
            attributes.attribute(rootProject.ext.jvmCompilerTypeAttr, 'jvm')
        }
    }
}

sourceSets.all { sourceSet ->
    sourceSet.runtimeClasspath += configurations.jvmImplementation
}

extensions.configure('jmh') {
    it.jmhVersion = 1.21
}

jmhJar {
    archiveBaseName.set('benchmarks')
    archiveClassifier.set(null)
    archiveVersion.set(null)
    configurations.add(project.configurations['jvmImplementation'])
}

dependencies {
    if (rootProject.ext.jvm_ir_target_enabled) {
        attributesSchema {
            attribute(rootProject.ext.jvmCompilerTypeAttr)
        }
    }

    annotationProcessor 'com.google.auto.value:auto-value:1.4.1'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-gson:0.4.6'

    compileOnly 'com.google.auto.value:auto-value:1.4.1'
    compileOnly 'com.ryanharter.auto.value:auto-value-gson:0.4.6'

    implementation "org.openjdk.jmh:jmh-core:1.21"
    implementation 'com.google.guava:guava:24.1.1-jre'

    implementation 'com.squareup.okio:okio:1.13.0'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.esotericsoftware:kryo:4.0.0'

    jvmImplementation project(':kotlinx-serialization-core')
    jvmImplementation project(':kotlinx-serialization-json')
    jvmImplementation project(':kotlinx-serialization-protobuf')
    jvmImplementation project(':kotlinx-serialization-json')

    // async profiler
    implementation group: 'pl.project13.scala', name: 'sbt-jmh-extras', version: '0.3.3'
    "jmhImplementation" sourceSets.main.runtimeClasspath
}
